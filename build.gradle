import java.time.ZoneId
import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id 'application'
}

repositories {
    jcenter()
    mavenCentral()
}

mainClassName = 'org.stapledon.photo.main.PhotoCatalogerAPI'

dependencies {
    implementation 'io.dropwizard:dropwizard-core:2.0.2'
    testImplementation 'io.dropwizard:dropwizard-testing:2.0.2'
    testImplementation 'com.google.code.gson:gson:2.8.6'

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
}

shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
    manifest {
        attributes 'Implementation-Title': rootProject.name
        attributes 'Implementation-Version': rootProject.version
        attributes 'Implementation-Vendor-Id': rootProject.group
        attributes 'Build-Time': ZonedDateTime.now(ZoneId.of("UTC")).format(DateTimeFormatter.ISO_ZONED_DATE_TIME)
        attributes 'Built-By': InetAddress.localHost.hostName
        attributes 'Created-By': 'Gradle ' + gradle.gradleVersion
        attributes 'Main-Class': mainClassName
    }
}

runShadow {
    args = ['server', "${project.rootDir}/config.yml"]
}